#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = AnalisadorLexico
ParserName = AnalisadorSintatico
SemanticName = AnalisadorSemantico
Package = br.com.unisul.analisador.motor
ScannerCaseSensitive = false
ScannerTable = Full
Input = Stream
Parser = LL
#RegularDefinitions
L	: [A-Za-z]
D	: [0-9]
WS	: [\ \t\n\r] //simbolos especiais (white space)
COMMENT	: "(*" [^ "*" ]* "*)" //comentarios
#Tokens
//pontuacao
"+"
"-"
"*"
"/"
"("
")"
":="
":"
"="
">"
"<"
"<="
">="
"<>"
","
";"
"."

ID 	: {L} ( {L} | {D} | _ )* //expressão regular
INTEIRO 	: {D}+ //um ou mais digitos
LIT	: \"(.+)\"

PROGRAM		= ID : "PROGRAM"
CONST		= ID : "CONST"
VAR		= ID : "VAR"
PROCEDURE	= ID : "PROCEDURE"
BEGIN		= ID : "BEGIN"
END		= ID : "END"
INTEGER		= ID : "INTEGER"
CALL		= ID : "CALL"
IF		= ID : "IF"
THEN		= ID : "THEN"
ELSE		= ID : "ELSE"
WHILE		= ID : "WHILE"
DO		= ID : "DO"
REPEAT		= ID : "REPEAT"
UNTIL		= ID : "UNTIL"
READLN		= ID : "READLN"
WRITELN		= ID : "WRITELN"
OR		= ID : "OR"
AND		= ID : "AND"
NOT		= ID : "NOT"
FOR		= ID : "FOR"
TO		= ID : "TO"
CASE		= ID : "CASE"
OF		= ID : "OF"

//ignorar campos em branco, caracteres especiais
 : {WS}
 : !{COMMENT}
#NonTerminals
<PROGRAMA>
<BLOCO>
<LID>
<REPID>
<DCLCONST>
<LDCONST>
<DCLVAR>
<LDVAR>
<TIPO>
<DCLPROC>
<DEFPAR>
<CORPO>
<REPCOMANDO>
<COMANDO>
<PARAMETROS>
<REPPAR>
<ELSEPARTE>
<VARIAVEL>
<REPVARIAVEL>
<ITEMSAIDA>
<REPITEM>
<CONDCASE>
<RPINTEIRO>
<CONTCASE>
<EXPRESSAO>
<REPEXPSIMP>
<EXPSIMP>
<REPEXP>
<TERMO>
<REPTERMO>
<FATOR>

#Grammar
<PROGRAMA> 	::= PROGRAM ID #100 ";" <BLOCO> "." #101 ;
<BLOCO> 		::= <DCLCONST> <DCLVAR> #102 <DCLPROC><CORPO> ;
<LID> 		::= ID #104 <REPID> ;
<REPID> 		::= î ;
<REPID>		::= "," ID #104 <REPID> ;
<DCLCONST>	::= CONST ID #105 "=" INTEIRO #106 ";" <LDCONST> ;
<LDCONST>	::= î ;
<LDCONST>	::= ID #105 "=" INTEIRO #106 ";" <LDCONST> ;
<DCLCONST>	::= î ;
<DCLVAR>	::= VAR #107 <LID> ":" <TIPO> ";" <LDVAR> ;
<LDVAR>		::= î ;
<LDVAR>		::= <LID> ":" <TIPO> ";" <LDVAR> ;
<DCLVAR> 	::= î ;
<TIPO> 		::= INTEGER ;
<DCLPROC> 	::= PROCEDURE ID #108 <DEFPAR> ";" #109 <BLOCO> ";" #110 <DCLPROC> ;
<DCLPROC> 	::= î ;
<DEFPAR> 	::= î ;
<DEFPAR> 	::= "(" #111 <LID> ":" INTEGER ")" ;
<CORPO> 		::= BEGIN <COMANDO> <REPCOMANDO> END ;
<REPCOMANDO> 	::= î ;
<REPCOMANDO>	::= ";" <COMANDO> <REPCOMANDO> ;
<COMANDO> 	::= ID #114 ":=" <EXPRESSAO> #115 ;
<COMANDO> 	::= <CORPO> ;
<COMANDO>	::= î ;
<COMANDO> 	::= CALL ID #116 <PARAMETROS> #117 ;
<PARAMETROS> 	::= î ;
<PARAMETROS> 	::= "(" <EXPRESSAO> #118 <REPPAR> ")" ;
<REPPAR> 	::= î ;
<REPPAR> 	::= "," <EXPRESSAO>#118 <REPPAR> ;
<COMANDO> 	::= IF <EXPRESSAO> #120 THEN <COMANDO> <ELSEPARTE> #121 ;
<ELSEPARTE> 	::= î ;
<ELSEPARTE> 	::= #122 ELSE <COMANDO> ;
<COMANDO> 	::= WHILE #123 <EXPRESSAO> #124 DO <COMANDO> #125 ;
<COMANDO> 	::= REPEAT #126 <COMANDO> UNTIL <EXPRESSAO> #127 ;
<COMANDO> 	::= READLN #128 "(" <VARIAVEL> <REPVARIAVEL> ")" ;
<VARIAVEL> 	::= ID #129 ;
<REPVARIAVEL>	::= î ;
<REPVARIAVEL>	::= "," <VARIAVEL> <REPVARIAVEL> ;
<COMANDO> 	::= WRITELN "(" <ITEMSAIDA> <REPITEM> ")" ;
<ITEMSAIDA> 	::= LIT #130 ;
<ITEMSAIDA> 	::= <EXPRESSAO> #131 ;
<REPITEM> 	::= î ;
<REPITEM> 	::= "," <ITEMSAIDA> <REPITEM> ;
<COMANDO> 	::= CASE #132 <EXPRESSAO> OF <CONDCASE> END #133 ;
<CONDCASE> 	::= INTEIRO <RPINTEIRO> ":" #134 <COMANDO> #135 <CONTCASE> ;
<RPINTEIRO>	::= "," #136 INTEIRO <RPINTEIRO> ;
<RPINTEIRO> 	::= î ;
<CONTCASE> 	::= î ;
<CONTCASE>	::= ";" <CONDCASE> ;
<COMANDO> 	::= FOR ID #137 ":=" <EXPRESSAO> #138 TO <EXPRESSAO> #139 DO <COMANDO> #140 ;
<EXPRESSAO> 	::= <EXPSIMP> <REPEXPSIMP> ;
<REPEXPSIMP> 	::= î ;
<REPEXPSIMP>	::= "=" <EXPSIMP> #141 ;
<REPEXPSIMP> 	::= "<" <EXPSIMP> #142 ;
<REPEXPSIMP> 	::= ">" <EXPSIMP> #143 ;
<REPEXPSIMP> 	::= ">=" <EXPSIMP> #144 ;
<REPEXPSIMP> 	::= "<=" <EXPSIMP> #145 ;
<REPEXPSIMP> 	::= "<>" <EXPSIMP> #146 ;
<EXPSIMP>	::= "+" <TERMO> <REPEXP> ;
<EXPSIMP>	::= "-" <TERMO> #147 <REPEXP> ;
<EXPSIMP> 	::= <TERMO> <REPEXP> ;
<REPEXP> 	::= "+" <TERMO> #148 <REPEXP> ;
<REPEXP> 	::= "-" <TERMO> #149 <REPEXP> ;
<REPEXP> 	::= OR <TERMO> #150 <REPEXP> ;
<REPEXP> 	::= î ;
<TERMO> 		::= <FATOR> <REPTERMO> ;
<REPTERMO> 	::= î ;
<REPTERMO> 	::= "*" <FATOR> #151 <REPTERMO> ;
<REPTERMO> 	::= "/" <FATOR> #152 <REPTERMO> ;
<REPTERMO> 	::= AND <FATOR> #153 <REPTERMO> ;
<FATOR> 		::= INTEIRO #154 ;
<FATOR> 		::= "(" <EXPRESSAO> ")" ;
<FATOR> 		::= NOT <FATOR> #155 ;
<FATOR> 		::= #156 <VARIAVEL> ;
